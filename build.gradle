plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'application'
    id 'groovy'
}

group = 'soy.frank'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'io.cucumber:cucumber-groovy:6.1.1'
    testImplementation 'io.cucumber:cucumber-junit:6.8.1'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.7'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = 'io.cucumber.core.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'soy.frank.rayTracer', 'src/test/resources']
        }
    }
}
compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '11'
}

application {
    mainClass.set('soy.frank.rayTracer.MainKt')
}

test {
    systemProperty("cucumber.execution.parallel.enabled", System.getProperty("test.parallel"))
    systemProperty("cucumber.filter.tags", "not @ignore")
    useJUnitPlatform()
}